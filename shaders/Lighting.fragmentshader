
in vec2 UV;
out vec4 OutColor;


uniform sampler2D gColor;
uniform sampler2D gPosition;
uniform sampler2D gNormal;
uniform sampler2D shadowMap;
uniform sampler2D Ssao;


uniform int LightCount;

uniform vec3 LightPositions[MAX_LIGHTS];
uniform vec3 LightColors[MAX_LIGHTS];

uniform mat4 ShadowMVP;

uniform vec3 CameraP;

int AoBlurSize = 4; // size of AO noise texture
float BlurredAO = 0.0f;

vec2 poissonDisk[16] = vec2[](
   vec2( -0.94201624, -0.39906216 ),
   vec2(  0.94558609, -0.76890725 ),
   vec2( -0.09418410, -0.92938870 ),
   vec2(  0.34495938,  0.29387760 ),
   vec2( -0.91588581,  0.45771432 ),
   vec2( -0.81544232, -0.87912464 ),
   vec2( -0.38277543,  0.27676845 ),
   vec2(  0.97484398,  0.75648379 ),

   vec2(  0.44323325, -0.97511554 ),
   vec2(  0.53742981, -0.47373420 ),
   vec2( -0.26496911, -0.41893023 ),
   vec2(  0.79197514,  0.19090188 ),
   vec2( -0.24188840,  0.99706507 ),
   vec2( -0.81409955,  0.91437590 ),
   vec2(  0.19984126,  0.78641367 ),
   vec2(  0.14383161, -0.14100790 )
);






// Tuning
  float gamma = 2.2f;
  float GlobalLightIntensity = 0.05f; // 1.0 represents full sun

  float materialShininess = 15.0f;
//


void main()
{
  vec3 Diffuse      = texture(gColor, UV).rgb;
  float Emission    = texture(gColor, UV).a;

  vec3 FragNormal   = texture(gNormal, UV).rgb;   // modelspace
  vec3 FragPosition = texture(gPosition, UV).rgb; // worldspace

  vec3 AmbientLight = Diffuse * GlobalLightIntensity * Emission;

  vec3 AccumulatedLight = AmbientLight;

  for ( int LightIndex = 0;
      LightIndex < LightCount;
      ++LightIndex)
  {
    vec3 LightPosition = LightPositions[LightIndex];
    vec3 LightColor = LightColors[LightIndex];

    vec3 FragToLight = normalize(LightPosition - FragPosition);

    float LightCosTheta = clamp( dot( FragNormal, FragToLight), 0.0f, 1.0f);

    float Distance = distance(FragPosition, LightPosition);
    float LightAtt = 1.0f/(Distance*Distance);


    vec3 reflectionVector = reflect(FragToLight, FragNormal);
    vec3 FragToCamera = normalize(FragPosition - CameraP);
    float cosAngle = max(0.0, dot(FragToCamera, reflectionVector));
    float SpecularPower = pow(cosAngle, materialShininess);


    vec3 DirectLight = (Diffuse + LightColor) * LightCosTheta * LightAtt;
    vec3 SpecularLight = (Diffuse + LightColor) * SpecularPower * LightAtt;


    AccumulatedLight += DirectLight + SpecularLight;
  }


#if USE_AO_SHADER
  vec2 texelSize = 1.0 / vec2(textureSize(Ssao, 0));
  float AccumAO = 0.0;

  vec2 hlim = vec2(float(-AoBlurSize) * 0.5 + 0.5);

  for (int i = 0; i < AoBlurSize; ++i) {
     for (int j = 0; j < AoBlurSize; ++j) {
        vec2 TexOffset = (hlim + vec2(float(i), float(j))) * texelSize;
        AccumAO += texture(Ssao, UV + TexOffset).r;
     }
  }

  BlurredAO = AccumAO / float(AoBlurSize * AoBlurSize);
#else
  BlurredAO = 1.0f;
#endif


  vec3 Visibility = vec3(1.0f);

#if USE_SHADOW_MAPPING
  /*
   *  Shadow Mapping
   */
  vec4 FragPShadowScreenSpace = ShadowMVP * vec4(FragPosition, 1.0f);

  float FragPShadowDepth = FragPShadowScreenSpace.z;

  float acneBias = 0.001f; // Fix acne
  /* float ShadowDepth = texture(shadowMap, vec3(FragPShadowScreenSpace.xy, FragPShadowScreenSpace.z/FragPShadowScreenSpace.w)) + acneBias; */
  float ShadowDepth = texture(shadowMap, FragPShadowScreenSpace.xy).x + acneBias;

  if ( FragPShadowDepth > ShadowDepth )
    Visibility = vec3(0.1f);

  /* float Visibility = 1.0f; */
  /* for (int i=0;i < 8;i++){ */
  /*   int index = i * 2; */
  /*   Visibility -= 0.1*( 1.0 - texture(shadowMap, vec3(ShadowCoord.xy + poissonDisk[index]/1000.0,  (ShadowCoord.z-bias)/ShadowCoord.w) )); */
  /* } */
#endif

  /*
   *  Tone-mapping && gamma correction
   */
  vec3 TotalLight = BlurredAO * Visibility * AccumulatedLight;


  // Reinhard tone mapping
  vec3 ToneMapped = TotalLight / (TotalLight + vec3(1.0f));

  // Gamma correction
  OutColor = vec4(pow(ToneMapped, vec3(1.0f / gamma)), 1.0f);
}
